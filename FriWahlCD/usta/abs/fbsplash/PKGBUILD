# Maintainer: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Kurt J. Bosch <kjb-temp-2009 at alpenjodel.de>

pkgname=fbsplash
pkgver=1.5.4.4
pkgrel=7
pkgdesc="A userspace implementation of a splash screen for Linux (formerly known as gensplash)"
arch=('i686' 'x86_64')
url="http://fbsplash.alanhaggai.org"
license=('GPL')
depends=('miscsplashutils' 'freetype2' 'libjpeg' 'libpng' 'libmng' 'lcms' 'gpm') 
optdepends=('linux-fbcondecor: enable console background images'
            'fbsplash-extras: additional functionality like daemon icons'
            'uswsusp-fbsplash: suspend to disk with fbsplash'
            'python: convert themes from splashy to fbsplash')
conflicts=('fbsplash-scripts' 'initscripts-extras-fbsplash')
options=('!makeflags')
backup=('etc/conf.d/fbcondecor' 'etc/conf.d/splash')
install=fbsplash.install
source=(http://fbsplash.alanhaggai.org/tarballs/files/splashutils-${pkgver}.tar.bz2
        splash_start_initcpio.patch
        splash.conf
        fbsplash-basic.sh
        fbsplash.initcpio_install
        fbsplash.initcpio_hook
        fbcondecor.daemon
        fbcondecor.conf)
md5sums=('2a16704c4adde97b58812cd89e3f2342'
         '4045e315c52f5a576fca4f7e634eeb91'
         '90708a96038d7d7921c2e9fde938c058'
         'a6b0b9c5c7a460380a7db127e0278164'
         '24e2a7bbc2d18fe3ec10093665cd7a6e'
         '4a3ac992a5042fa9770cbd3fdb7e1465'
         '715926469d05eecd345c052e6248ca32'
         'b3db9d4fd902b62ac9e38589677e2d16')

build() {
  cd ${srcdir}/splashutils-${pkgver}

  autoreconf

  # fix fbcondecor_ctl splash type
  sed -e 's,fbsplash_lib_init(fbspl_bootup),fbsplash_lib_init(fbspl_undef),' -i src/fbcon_decor_ctl.c

  # fix set_event_dev call for initcpio usage (if evdev module is there)
  #patch -Np2 -i ${srcdir}/splash_start_initcpio.patch

  export LIBS="-lbz2"
  ./configure --prefix=/usr --sysconfdir=/etc --without-klibc --enable-fbcondecor --with-gpm --with-mng --with-png --with-ttf --with-ttf-kernel
  make
}

package() {
  cd ${srcdir}/splashutils-${pkgver}

  make DESTDIR=${pkgdir} install

  cd ${pkgdir}

  # fix duplicate slashes to get splash_cache_cleanup grep to work
  sed -r -e 's,^(export spl_.*="/)/+,\1,' -i sbin/splash-functions.sh

  # fix the path to splash_util
  sed -r -e 's,^(export spl_util=)\"/bin/,\1"/sbin/,' -i sbin/splash-functions.sh

  # provide the mountpoint needed by splash-functions.sh
  mkdir -p lib/splash/{cache,tmp}

  # Install fbsplash scripts and config file
  install -D -m644 ${srcdir}/splash.conf etc/conf.d/splash
  install -D -m644 ${srcdir}/fbsplash-basic.sh etc/rc.d/functions.d/fbsplash-basic.sh
  install -D -m644 ${srcdir}/fbsplash.initcpio_install lib/initcpio/install/fbsplash
  install -D -m644 ${srcdir}/fbsplash.initcpio_hook lib/initcpio/hooks/fbsplash

  # Install fbcodecor script and config file
  install -D -m644 ${srcdir}/fbcondecor.conf etc/conf.d/fbcondecor
  install -D -m755 ${srcdir}/fbcondecor.daemon etc/rc.d/fbcondecor
}
